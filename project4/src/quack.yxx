%skeleton "lalr1.cc"
%require  "3.0"

// This is how to get the header files into the parser as well
// as include whatever you wish in the files it generates
%code requires {
  namespace yy {
    class Lexer;  // RE/Flex generates this class with namespace=yy lexer=Lexer
  }
  #include "ASTNode.h"  // Including the AST Classes
}

%locations
%define parse.trace

%parse-param { yy::Lexer& lexer }  // Construct parser object with lexer
%parse-param { AST::Node** root }  // To pass AST root back to driver

%code {
    #include "lex.yy.h"
    #undef yylex
    #define yylex lexer.yylex  // Within Bison's parse() we should invoke lexer.yylex(), not the global yylex()

    // this is used to simplify the grammar, as many call nodes 
    // use the same format to construct themselves for the AST
    AST::Node *getCallNode(Type callType, std::string callStr, 
                            AST::Node *dollarOne, AST::Node *dollarThree) {
        AST::Node *retVal = new AST::Node(CALL); 
        retVal->subType = callType; 
        retVal->insert(dollarOne);
        AST::Node *method = new AST::Node(IDENT, callStr); 
        method->subType = METHOD; 
        retVal->insert(method);
        AST::Node *actuals = new AST::Node(ACTUAL_ARGS);
        actuals->insert(dollarThree);
        retVal->insert(actuals);
        return retVal;
    }
}

%union { // all the types a non-terminal or token can assume
    int   num;
    char* str;
    AST::Node* node;
}

// -~-~-~- TOKENS -~-~-~-
%token <str> STRING_LIT
%token <num> INT_LIT

%token CLASS DEF EXTENDS
%token IF ELIF ELSE
%token WHILE RETURN TYPECASE
%token ATLEAST ATMOST EQUALS
%token AND OR NOT
%token IDENT
%token NEG

// -~-~-~- AST NODES -~-~-~-
%type <str> IDENT
%type <node> program
%type <node> classes
%type <node> class
%type <node> extends
%type <node> formal_args
%type <node> args arg
%type <node> stmt_block
%type <node> stmts
%type <node> stmt
%type <node> opt_elif
%type <node> typecase
%type <node> type_alts
%type <node> type_alt
%type <node> r_expr
%type <node> actual_args
%type <node> actuals actual
%type <node> l_expr
%type <node> method
%type <node> methods
%type <node> ident

// -~-~-~- PRECEDENCE -~-~-~-
%left AND OR NOT
%left EQUALS ATMOST ATLEAST '<' '>'
%left '-' '+'
%left '*' '/'
%left '.'
%left UMINUS

// -~-~-~-~-~-~-~-~-~-~-~-~ GRAMMAR -~-~-~-~-~-~-~-~-~-~-~-~

%%

// Each program is any number of classes followed by any number of statements.
// Note: statements may not contain method definitions, only classes can.
program
        : classes stmts { *root = new AST::Node(PROGRAM); $$ = *root; 
                          $$->insert($1); $$->insert($2); }
        ;

// -~-~-~-~-~-~-~-~-~-~-~-~ Classes -~-~-~-~-~-~-~-~-~-~-~-~
// The class rule creates a special structure in the AST.
// This structure is inside a special node type called "constructor",
// which is a type of method. Basically, it wraps the constructor
// of a class in a method, with assumed information added (return type, name).

classes // a sequence type that allows us to bundle classes together
        : classes class { $$->insert($2); $$ = $1; }
        | /* empty */   { $$ = new AST::Node(CLASSES); }
        ;

class
        : CLASS ident '(' formal_args ')' extends '{' stmts methods '}' 
            { $$ = new AST::Node(CLASS); /*'*/
              $2->subType = CLASS_NAME; $$->insert($2); 
              $$->insert($6); 
              AST::Node *method = new AST::Node(METHOD); 
              method->subType = CONSTRUCTOR;
              AST::Node *methodname = new AST::Node(IDENT, METHOD_NAME, $2->name);
              method->insert(methodname);
              AST::Node *ret = new AST::Node(IDENT, RETURN_TYPE, $2->name);
              method->insert($4);
              method->insert(ret);
              method->insert($8);
              $$->insert(method);
              $$->insert($9); }
        ;

extends
        : EXTENDS ident { $2->subType = SUPER_NAME; $$ = $2; }
        | /* empty */ { $$ = new AST::Node(IDENT, SUPER_NAME, "Obj"); }
        ;

// -~-~-~-~-~-~-~-~-~-~-~-~ Methods -~-~-~-~-~-~-~-~-~-~-~-~
// Note: methods may only be declared in classes
methods
        : methods method { $1->insert($2); $$ = $1; }
        | /* empty */ { $$ = new AST::Node(METHODS); }
        ;

method
        : DEF ident '(' formal_args ')' stmt_block 
                            { $$ = new AST::Node(METHOD); $2->subType = METHOD_NAME; 
                              $$->insert($2); $$->insert($4); 
                              AST::Node *ret = new AST::Node(IDENT, RETURN_TYPE, "Nothing");
                              $$->insert(ret); 
                              $6->subType = STATEMENTS; $$->insert($6); }
        | DEF ident '(' formal_args ')' ':' ident stmt_block 
                            { $$ = new AST::Node(METHOD); 
                              $2->subType = METHOD_NAME; $$->insert($2); 
                              $$->insert($4); $7->subType = RETURN_TYPE; $$->insert($7); 
                              $8->subType = STATEMENTS; $$->insert($8); }
        ;

// -~-~-~-~-~-~-~-~-~-~-~-~ Formal Args -~-~-~-~-~-~-~-~-~-~-~-~
// Formal args appear in both class definitions and method definitions.

formal_args // a sequence type
        : args { $$ = $1; }
        | /* empty */ { $$ = new AST::Node(FORMAL_ARGS); }
        ;

args
        : arg { $$ = new AST::Node(FORMAL_ARGS); $$->insert($1); }
        | args ',' arg { $$ = $1; $1->insert($3); }
        ;

arg
        : ident ':' ident { $$ = new AST::Node(CLASS_ARG); $1->subType = VAR_IDENT; $$->insert($1); 
                            $3->subType = TYPE_IDENT; $$->insert($3); }
        ;

// -~-~-~-~-~-~-~-~-~-~-~-~ Statements -~-~-~-~-~-~-~-~-~-~-~-~

stmt_block
        : '{' stmts '}' { $$ = $2; }
        ;

stmts
        : stmts stmt { $1->insert($2); $$ = $1; }
        | /* empty */ { $$ = new AST::Node(BLOCK); $$->subType = STATEMENTS; }
        ;

// -~-~-~- Statements : Control flow
// -~-~-~- Control flow : if/else
stmt
        : IF r_expr stmt_block opt_elif { $$ = new AST::Node(IF); $$->insert($2); 
                                          $3->subType = TRUE_STATEMENTS; $$->insert($3); $$->insert($4); }
        ;

opt_elif:  
        ELIF r_expr stmt_block opt_elif { $$ = new AST::Node(BLOCK); $$->subType = FALSE_STATEMENTS;
                                          AST::Node *newif = new AST::Node(IF); newif->insert($2); 
                                          $3->subType = TRUE_STATEMENTS; newif->insert($3); 
                                          newif->insert($4); $$->insert(newif); }
        | ELSE stmt_block   { $2->subType = FALSE_STATEMENTS; $$ = $2; }
        | /* empty */       { $$ = new AST::Node(BLOCK); $$->subType = FALSE_STATEMENTS; }
        ;

// -~-~-~- Control flow : while loop
stmt
        : WHILE r_expr stmt_block { $$ = new AST::Node(WHILE); $$->insert($2); $$->insert($3); }
        ;

// -~-~-~- Statements : Other
stmt
        : l_expr '=' r_expr ';' { $$ = new AST::Node(ASSIGN); $$->insert($1); $3->subType = R_EXPR; $$->insert($3); }
        | l_expr ':' ident '=' r_expr ';' { $$ = new AST::Node(ASSIGN); $$->insert($1); $$->insert($3); 
                                            $5->subType = R_EXPR; $$->insert($5); }
        | r_expr ';' { $$ = $1; }
        | RETURN r_expr ';' { $$ = new AST::Node(RETURN); $2->subType = R_EXPR; $$->insert($2); }
        | RETURN ';' { $$ = new AST::Node(RETURN); $$->insert(new AST::Node(IDENT, "Nothing")); }
        | typecase { $$ = $1; }
        ;

// -~-~-~-~-~-~-~-~-~-~-~-~ Typecase -~-~-~-~-~-~-~-~-~-~-~-~

typecase
        : TYPECASE r_expr '{' type_alts '}' { $$ = new AST::Node(TYPECASE); 
                                              $$->insert($2); $$->insert($4); }
        ;

type_alts
        : type_alts type_alt { $1->insert($2); $$ = $1; }
        | /* empty */        { $$ = new AST::Node(TYPE_ALTERNATIVES); }
        ;

type_alt 
        : ident ':' ident stmt_block { $$ = new AST::Node(TYPE_ALTERNATIVE); 
                                       $$->insert($1); $$->insert($3); $$->insert($4); }
        ;

// -~-~-~-~-~-~-~-~-~-~-~-~ r_expr and l_expr -~-~-~-~-~-~-~-~-~-~-~-~

r_expr
        : STRING_LIT { $$ = new AST::Node(STRCONST, std::string($1)); }
        | INT_LIT { $$ = new AST::Node(INTCONST, $1); }
        | l_expr { $$ = new AST::Node(LOAD); $$->insert($1); $$->subType = L_EXPR; }
        | '-' r_expr %prec UMINUS { AST::Node *zero = new AST::Node(INTCONST, 0);
                                    $$ = getCallNode(MINUS, "MINUS", zero, $2); }
        | r_expr '*' r_expr { $$ = getCallNode(MULT, "MULT", $1, $3); }
        | r_expr '/' r_expr { $$ = getCallNode(DIV, "DIV", $1, $3); }
        | r_expr '+' r_expr { $$ = getCallNode(PLUS, "PLUS", $1, $3); }
        | r_expr '-' r_expr { $$ = getCallNode(MINUS, "MINUS", $1, $3); }
        | r_expr EQUALS r_expr { $$ = getCallNode(EQ, "EQ", $1, $3); }
        | r_expr ATMOST r_expr { $$ = getCallNode(ATMOST, "ATMOST", $1, $3); }
        | r_expr '<' r_expr { $$ = getCallNode(LESSER, "LESSER", $1, $3); }
        | r_expr ATLEAST r_expr { $$ = getCallNode(ATLEAST, "ATLEAST", $1, $3); }
        | r_expr '>' r_expr { $$ = getCallNode(GREATER, "GREATER", $1, $3); }
        | r_expr AND r_expr { $$ = getCallNode(AND, "AND", $1, $3); }
        | r_expr OR r_expr { $$ = getCallNode(OR, "OR", $1, $3); }
        | NOT r_expr { $$ = new AST::Node(NOT); $$->insert($2); } 
        | '(' r_expr ')' { $$ = $2; }
        | r_expr '.' ident '(' actual_args ')' { $$ = new AST::Node(CALL); $$->insert($1); $3->subType = METHOD;
                                                 $$->insert($3); $$->insert($5); }
        | ident '(' actual_args ')' { $$ = new AST::Node(CONSTRUCTOR); $$->insert($1); $$->insert($3); }
        ;

l_expr
        : ident { $$ = $1; $$->subType = LOC; /* new AST::Node(LOAD); $$->insert($1); */ }
        | r_expr '.' ident { $$ = new AST::Node(DOT); $$->subType = L_EXPR; $$->insert($1); 
                             $3->subType = R_EXPR; $$->insert($3); }
        ;

// -~-~-~-~-~-~-~-~-~-~-~-~ Actual args -~-~-~-~-~-~-~-~-~-~-~-~

actual_args
        : actuals { $$ = $1; }
        | /* empty */ { $$ = new AST::Node(ACTUAL_ARGS); }
        ;

actuals
        : actual { $$ = new AST::Node(ACTUAL_ARGS); $$->insert($1); }
        | actuals ',' actual { $$ = $1; $1->insert($3); }
        ;

actual
        : r_expr { $$ = new AST::Node(METHOD_ARG); $$->insert($1); }
        ;

// -~-~-~-~-~-~-~-~-~-~-~-~ Ident NT -~-~-~-~-~-~-~-~-~-~-~-~

ident
        : IDENT { $$ = new AST::Node(IDENT, $1); }
        ;

%%

// -~-~-~- -~-~-~- -~-~-~-~

#include "Messages.h"

void yy::parser::error(const location_type& loc, const std::string& msg) {
    report::error_at(loc, msg, PARSER);
}